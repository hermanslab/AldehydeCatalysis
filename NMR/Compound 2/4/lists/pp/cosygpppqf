;cosygpppqf
;avance-version (12/01/11)
;2D homonuclear shift correlation
;using gradient pulses for selection
;using purge pulses before d1
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=
;$RECOMMEND=y


#include <Avance.incl>
#include <Grad.incl>


"d11=30m"
"d12=20u"
"d13=4u"


"in0=inf1"

"d0=3u"


1 ze
2 d11 

3 d12 pl10:f1
  p17 ph3
  p17*2 ph4
  d1 pl1:f1

  p1 ph1
  d0
  50u UNBLKGRAD
  p16:gp1
  d16
  p0 ph2
  d13
  p16:gp1
  d16
  4u BLKGRAD
  go=2 ph31
  d11 mc #0 to 2 F1QF(caldel(d0, +in0))
exit


ph1=0 2
ph2=0 0 2 2
ph3=0
ph4=1
ph31=0 2


;pl1 : f1 channel - power level for pulse (default)
;pl10: f1 channel - power level for TOCSY-spinlock
;p0 : f1 channel -  20 to 90 degree high power pulse
;p1 : f1 channel -  90 degree high power pulse
;p16: homospoil/gradient pulse
;p17: f1 channel - trim pulse                             [2.5 msec]
;d0 : incremented delay (2D)                              [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                                  [30 msec]
;d12: delay for power switching                           [20 usec]
;d13: short delay                                         [4 usec]
;d16: delay for homospoil/gradient recovery
;inf1: 1/SW = 2 * DW
;in0: 1/(1 * SW) = 2 * DW
;nd0: 1
;ns: 1 * n
;ds: 16
;td1: number of experiments
;FnMODE: QF

;use gradient ratio:	gp 1
;			  10

;for z-only gradients:
;gpz1: 10%

;use gradient files:   
;gpnam1: SMSQ10.100



;$Id:$
