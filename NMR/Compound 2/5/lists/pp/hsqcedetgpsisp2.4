;hsqcedetgpsisp2.4
;avance-version (20/03/03)
;HSQC
;2D H-1/X correlation via double inept transfer
;   using sensitivity improvement
;phase sensitive using Echo/Antiecho-TPPI gradient selection
;with decoupling during acquisition
;using trim pulses in inept transfer
;with multiplicity editing during selection step
;using shaped pulses for all 180degree pulses on f2 - channel
;for matched sweep adiabatic pulses
;with gradients in back-inept
;with suppression of COSY type artefacts
;
;A.G. Palmer III, J. Cavanagh, P.E. Wright & M. Rance, J. Magn.
;   Reson. 93, 151-170 (1991)
;L.E. Kay, P. Keifer & T. Saarinen, J. Am. Chem. Soc. 114,
;   10663-5 (1992)
;J. Schleucher, M. Schwendinger, M. Sattler, P. Schmidt, O. Schedletzky,
;   S.J. Glaser, O.W. Sorensen & C. Griesinger, J. Biomol. NMR 4,
;   301-306 (1994)
;W. Willker, D. Leibfritz, R. Kerssebaum & W. Bermel, Magn. Reson.
;   Chem. 31, 287-292 (1993)
;C. Zwahlen, P. Legault, S.J.F. Vincent, J. Greenblatt, R. Konrat &
;   L.E. Kay, J. Am. Chem. Soc. 119 6711-6721 (1997)
;(R.D. Boyer, R. Johnson & K. Krishnamurthy, 
;   J. Magn. Reson. 165, 253-259 (2003) )
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"d2=1s/(cnst2*2)"
"d4=1s/(cnst2*4)"
"d11=30m"


"d0=3u"

"in0=inf1/2"


"DELTA=d21-p16-d16-p2-d0*2-p3*2/PI"
"DELTA1=d2-p16-d16-larger(p2,p14)/2-p1*0.78-4u"
"DELTA2=d4-larger(p2,p14)/2"
"DELTA3=d24-cnst17*p24/2-p19-d16"
"DELTA4=d4-larger(p2,p14)/2-p16-d16"
"DELTA5=d21-p16-d16-p2+p3*2/PI"
"DELTA6=d2-p16-d16-larger(p2,p14)/2-de-8u"


"TAU=larger(p2,p14)/2-p2/2"


"l0=0"

"td0=2"

"ns=ns/2"


"acqt0=0"
baseopt_echo


1 ze
  d11 pl12:f2
2 d1 do:f2 
3 (p1 ph1)
  DELTA2 pl0:f2
  4u
  (center (p2 ph1) (p14:sp3 ph6):f2 )
  4u
  DELTA2 pl2:f2 UNBLKGRAD

#   ifdef TRIMP
  p28 ph1
#   endif /* TRIMP */

  (p1 ph2) 
  
  (p3 ph3):f2
  d0 
  (p2 ph7)
  d0
  p16:gp1*EA
  d16
  DELTA
  (p31:sp18 ph1):f2
  (p2 ph1) 
  4u
  p16:gp1*EA*-1
  d16
  DELTA5
  (p31:sp18 ph1):f2
  2u
  2u pl2:f2
  (center (p1 ph1) (p3 ph4):f2 )
  p19:gp3
  d16
  DELTA3
  (center (p2 ph1) (p24:sp7 ph1):f2 )
  DELTA3
  p19:gp3
  d16 pl2:f2
  (center (p1 ph2) (p3 ph5):f2 )
  p16:gp4
  d16
  DELTA4 pl0:f2
  (center (p2 ph1) (p14:sp3 ph1):f2 )
  DELTA4
  p16:gp4
  d16
  (p1 ph1)
  4u
  p16:gp2*-1
  d16
  DELTA1

  if "l0 %2 == 0"
     {
     TAU
     (p2 ph1)
     TAU
     }
  else
     {
     (center (p2 ph1) (p14:sp3 ph1):f2 )
     }

  4u
  DELTA6
  p16:gp2
  d16 pl12:f2
  4u BLKGRAD
  go=2 ph31 cpd2:f2
  d1 do:f2 mc #0 to 2 
     F0(iu0 & ip31*2 & zd)
     F1EA(calgrad(EA) & calph(ph5, +180), caldel(d0, +in0) & calph(ph3, +180) & calph(ph6, +180) & calph(ph31, +180))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=0 0 2 2
ph5=3 3 1 1
ph6=0
ph7=0 0 2 2
ph31=2 0 0 2
  

;pl0 : 0W
;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;sp3: f2 channel - shaped pulse (180degree inversion)
;spnam3: Crp60,0.5,20.1
;sp7: f2 channel - shaped pulse (180degree refocussing)
;spnam7: Crp60comp.4
;sp18: f2 channel - shaped pulse 180 degree (adiabatic matched sweep)
;spnam18: Crp60_xfilt.2
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p14: f2 channel - 180 degree shaped pulse for inversion
;     = 500usec for Crp60,0.5,20.1
;p16: homospoil/gradient pulse
;p19: gradient pulse 2                                 [500 usec]
;p24: f2 channel - 180 degree shaped pulse for refocussing
;     = 2msec for Crp60comp.4
;p28: f1 channel - trim pulse                          [1 msec]
;p31: f2 channel - 180 degree shaped pulse for adiabatic matched sweep
;                      1730 us at 600.13 MHz, otherwise: 1730*sqrt(600/x)
;d0 : incremented delay (2D)                           [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d2 : 1/(2J)XH
;d4 : 1/(4J)XH
;d11: delay for disk I/O                               [30 msec]
;d16: delay for homospoil/gradient recovery
;d21: set d21 according to multiplicity selection
;        1/(2J(XH))  XH, XH3 positive, XH2 negative
;d24: 1/(8J)XH for all multiplicities
;     1/(4J)XH for XH
;cnst2: = J(XH)
;cnst17: = -0.5 for Crp60comp.4
;inf1: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(X)) = DW(X)
;nd0: 2
;ns: 1 * n
;ds: >= 16
;td0: 2
;td1: number of experiments
;FnMODE: echo-antiecho
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;for z-only gradients:
;gpz1: 80%
;gpz2: 20.1% for C-13, 8.1% for N-15
;gpz3: 11%
;gpz4: -5%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100


                                          ;preprocessor-flags-start
;TRIMP: to use trimpulse p28@pl1 start experiment with
;          option -DTRIMP (eda: ZGOPTNS)
                                          ;preprocessor-flags-end


;cnst17: Factor to compensate for coupling evolution during a pulse
;       (usually +1). A positive factor indicates that coupling 
;       evolution continues during the pulse, whereas a negative 
;       factor is necessary if the coupling is (partially) refocussed.



;$Id:$
